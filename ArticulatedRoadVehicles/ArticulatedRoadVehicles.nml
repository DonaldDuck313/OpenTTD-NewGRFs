grf{
    grfid: "GL\04\01";
    name: string(STR_GRF_NAME);
    desc: string(STR_GRF_DESCRIPTION);
    version: 2;
    min_compatible_version: 0;

    param 0{
        param_adjust_running_cost{
            name: string(STR_PARAM_NAME_ADJUST_RUNNING_COST);
            desc: string(STR_PARAM_DESC_ADJUST_RUNNING_COST);
            type: bool;
            def_value: 1;
        }
    }
}

cargotable{
    PASS,
    COAL, GRAI, IORE, WHEA, MAIZ, CORE, AORE, BEAN, CERE, CLAY, CMNT, GRVL, PORE, SAND,
    MAIL,
    OIL_, WATR, MILK,
    LVST,
    GOOD,
    WOOD, TWOD,
    STEL, COPR, ZINC, ALUM, IRON, METL, STAL, SCMT, STCB, STSE, STSH, STST, STWR, PAPR,
    VALU,
    FOOD,
    FRUT, FRVG, SGCN
}

template tmpl_front(y){
    //left x, upper y, width, height, offset x, offset y
    [0,       y,       10,    28,     -4,       -18]
    [20,      y,       26,    28,     -17,      -16]
    [50,      y,       36,    28,     -20,      -23]
    [90,      y,       26,    28,     -9,       -18]
    [120,     y,       10,    28,     -4,       -18]
    [140,     y,       26,    28,     -16,      -19]
    [170,     y,       36,    28,     -18,      -23]
    [210,     y,       26,    28,     -8,       -18]
}

template tmpl_back(y){
    //left x, upper y, width, height, offset x, offset y
    [240,     y,       10,    28,     -4,       -18]
    [260,     y,       26,    28,     -17,      -16]
    [290,     y,       36,    28,     -20,      -23]
    [330,     y,       26,    28,     -10,      -18]
    [240,     y,       10,    28,     -4,       -18]
    [260,     y,       26,    28,     -15,      -19]
    [290,     y,       36,    28,     -18,      -23]
    [330,     y,       26,    28,     -8,       -18]
}


//Bus
spriteset(spriteset_bus_front, "gfx/bus.png"){
    tmpl_front(0)
}

spriteset(spriteset_bus_back, "gfx/bus.png"){
    tmpl_back(0)
}

switch(FEAT_ROADVEHS, SELF, switch_bus_sprites, position_in_articulated_veh){
    0: return spriteset_bus_front;
    1: return spriteset_bus_back;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_bus, extra_callback_info1){
    1: return bus;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, bus){
    property{
        name: string(STR_BUS_NAME);
        introduction_date: date(2020, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 225;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 100 km/h;
        power: 300 hp;
        weight: 40 ton;
        cargo_capacity: 40;
        climates_available: ALL_CLIMATES;
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        default_cargo_type: PASS;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_bus_sprites;
        articulated_part: switch_articulated_bus;
    }
}


//Mineral truck
spriteset(spriteset_mineral_front, "gfx/mineral.png"){
    tmpl_front(0)
}

spriteset(spriteset_mineral_back, "gfx/mineral.png"){
    tmpl_back(0)
}

spriteset(spriteset_mineral_front_loaded_coal, "gfx/mineral.png"){
    tmpl_front(30)
}

spriteset(spriteset_mineral_back_loaded_coal, "gfx/mineral.png"){
    tmpl_back(30)
}

spriteset(spriteset_mineral_front_loaded_grain, "gfx/mineral.png"){
    tmpl_front(60)
}

spriteset(spriteset_mineral_back_loaded_grain, "gfx/mineral.png"){
    tmpl_back(60)
}

spriteset(spriteset_mineral_front_ore, "gfx/mineral.png"){
    tmpl_front(90)
}

spriteset(spriteset_mineral_back_ore, "gfx/mineral.png"){
    tmpl_back(90)
}

spriteset(spriteset_mineral_front_loaded_iron, "gfx/mineral.png"){
    tmpl_front(120)
}

spriteset(spriteset_mineral_back_loaded_iron, "gfx/mineral.png"){
    tmpl_back(120)
}

spriteset(spriteset_mineral_front_loaded_copper, "gfx/mineral.png"){
    tmpl_front(150)
}

spriteset(spriteset_mineral_back_loaded_copper, "gfx/mineral.png"){
    tmpl_back(150)
}

spriteset(spriteset_mineral_front_loaded_misc, "gfx/mineral.png"){
    tmpl_front(180)
}

spriteset(spriteset_mineral_back_loaded_misc, "gfx/mineral.png"){
    tmpl_back(180)
}

spritegroup spritegroup_mineral_front_coal{
    loading: [spriteset_mineral_front, spriteset_mineral_front_loaded_coal];
    loaded: [spriteset_mineral_front, spriteset_mineral_front_loaded_coal];
}

spritegroup spritegroup_mineral_back_coal{
    loading: [spriteset_mineral_back, spriteset_mineral_back_loaded_coal];
    loaded: [spriteset_mineral_back, spriteset_mineral_back_loaded_coal];
}

spritegroup spritegroup_mineral_front_grain{
    loading: [spriteset_mineral_front, spriteset_mineral_front_loaded_grain];
    loaded: [spriteset_mineral_front, spriteset_mineral_front_loaded_grain];
}

spritegroup spritegroup_mineral_back_grain{
    loading: [spriteset_mineral_back, spriteset_mineral_back_loaded_grain];
    loaded: [spriteset_mineral_back, spriteset_mineral_back_loaded_grain];
}

spritegroup spritegroup_mineral_front_iron{
    loading: [spriteset_mineral_front_ore, spriteset_mineral_front_loaded_iron];
    loaded: [spriteset_mineral_front_ore, spriteset_mineral_front_loaded_iron];
}

spritegroup spritegroup_mineral_back_iron{
    loading: [spriteset_mineral_back_ore, spriteset_mineral_back_loaded_iron];
    loaded: [spriteset_mineral_back_ore, spriteset_mineral_back_loaded_iron];
}

spritegroup spritegroup_mineral_front_copper{
    loading: [spriteset_mineral_front_ore, spriteset_mineral_front_loaded_copper];
    loaded: [spriteset_mineral_front_ore, spriteset_mineral_front_loaded_copper];
}

spritegroup spritegroup_mineral_back_copper{
    loading: [spriteset_mineral_back_ore, spriteset_mineral_back_loaded_copper];
    loaded: [spriteset_mineral_back_ore, spriteset_mineral_back_loaded_copper];
}

spritegroup spritegroup_mineral_front_misc{
    loading: [spriteset_mineral_front_ore, spriteset_mineral_front_loaded_misc];
    loaded: [spriteset_mineral_front_ore, spriteset_mineral_front_loaded_misc];
}

spritegroup spritegroup_mineral_back_misc{
    loading: [spriteset_mineral_back_ore, spriteset_mineral_back_loaded_misc];
    loaded: [spriteset_mineral_back_ore, spriteset_mineral_back_loaded_misc];
}

switch(FEAT_ROADVEHS, SELF, switch_mineral_front_sprites, cargo_type_in_veh){
    COAL: return spritegroup_mineral_front_coal;
    GRAI: return spritegroup_mineral_front_grain;
    IORE: return spritegroup_mineral_front_iron;
    WHEA: return spritegroup_mineral_front_grain;
    MAIZ: return spritegroup_mineral_front_grain;
    CORE: return spritegroup_mineral_front_copper;
    AORE: return spritegroup_mineral_front_misc;
    BEAN: return spritegroup_mineral_front_iron;
    CERE: return spritegroup_mineral_front_grain;
    CLAY: return spritegroup_mineral_front_copper;
    CMNT: return spritegroup_mineral_front_misc;
    GRVL: return spritegroup_mineral_front_misc;
    SAND: return spritegroup_mineral_front_grain;
    return spritegroup_mineral_front_misc;
}

switch(FEAT_ROADVEHS, SELF, switch_mineral_back_sprites, cargo_type_in_veh){
    COAL: return spritegroup_mineral_back_coal;
    GRAI: return spritegroup_mineral_back_grain;
    IORE: return spritegroup_mineral_back_iron;
    WHEA: return spritegroup_mineral_back_grain;
    MAIZ: return spritegroup_mineral_back_grain;
    CORE: return spritegroup_mineral_back_copper;
    AORE: return spritegroup_mineral_back_misc;
    BEAN: return spritegroup_mineral_back_iron;
    CERE: return spritegroup_mineral_back_grain;
    CLAY: return spritegroup_mineral_back_copper;
    CMNT: return spritegroup_mineral_back_misc;
    GRVL: return spritegroup_mineral_back_misc;
    SAND: return spritegroup_mineral_back_grain;
    return spritegroup_mineral_back_misc;
}

switch(FEAT_ROADVEHS, SELF, switch_mineral_sprites, position_in_articulated_veh){
    0: return switch_mineral_front_sprites;
    1: return switch_mineral_back_sprites;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_mineral, extra_callback_info1){
    1: return mineral;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, mineral){
    property{
        name: string(STR_MINERAL_NAME);
        introduction_date: date(2025, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 210;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 28;
        climates_available: ALL_CLIMATES;
        refittable_cargo_classes: bitmask(CC_BULK);
        non_refittable_cargo_classes: bitmask(CC_REFRIGERATED, CC_PIECE_GOODS, CC_LIQUID);
        cargo_disallow_refit: [GRAI, WHEA, MAIZ, CERE, SCMT, SGCN];
        default_cargo_type: COAL;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_mineral_sprites;
        articulated_part: switch_articulated_mineral;
    }
}


//Mail truck
spriteset(spriteset_mail_front, "gfx/mail.png"){
    tmpl_front(0)
}

spriteset(spriteset_mail_back, "gfx/mail.png"){
    tmpl_back(0)
}

switch(FEAT_ROADVEHS, SELF, switch_mail_sprites, position_in_articulated_veh){
    0: return spriteset_mail_front;
    1: return spriteset_mail_back;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_mail, extra_callback_info1){
    1: return mail;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, mail){
    property{
        name: string(STR_MAIL_NAME);
        introduction_date: date(2030, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 220;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 30;
        climates_available: ALL_CLIMATES;
        refittable_cargo_classes: bitmask(CC_MAIL);
        default_cargo_type: MAIL;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_mail_sprites;
        articulated_part: switch_articulated_mail;
    }
}


//Tanker truck
spriteset(spriteset_oil_tanker_front, "gfx/tanker.png"){
    tmpl_front(0)
}

spriteset(spriteset_oil_tanker_back, "gfx/tanker.png"){
    tmpl_back(0)
}

spriteset(spriteset_water_tanker_front, "gfx/tanker.png"){
    tmpl_front(30)
}

spriteset(spriteset_water_tanker_back, "gfx/tanker.png"){
    tmpl_back(30)
}

switch(FEAT_ROADVEHS, SELF, switch_tanker_front_sprites, cargo_type_in_veh){
    WATR: return spriteset_water_tanker_front;
    MILK: return spriteset_water_tanker_front;
    return spriteset_oil_tanker_front;
}

switch(FEAT_ROADVEHS, SELF, switch_tanker_back_sprites, cargo_type_in_veh){
    WATR: return spriteset_water_tanker_back;
    MILK: return spriteset_water_tanker_back;
    return spriteset_oil_tanker_back;
}

switch(FEAT_ROADVEHS, SELF, switch_tanker_sprites, position_in_articulated_veh){
    0: return switch_tanker_front_sprites;
    1: return switch_tanker_back_sprites;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_tanker, extra_callback_info1){
    1: return tanker;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, tanker){
    property{
        name: string(STR_TANKER_NAME);
        introduction_date: date(2018, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 220;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 30;
        climates_available: ALL_CLIMATES;
        refittable_cargo_classes: bitmask(CC_LIQUID);
        default_cargo_type: OIL_;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_tanker_sprites;
        articulated_part: switch_articulated_tanker;
    }
}


//Livestock truck
spriteset(spriteset_livestock_front, "gfx/livestock.png"){
    tmpl_front(0)
}

spriteset(spriteset_livestock_back, "gfx/livestock.png"){
    tmpl_back(0)
}

switch(FEAT_ROADVEHS, SELF, switch_livestock_sprites, position_in_articulated_veh){
    0: return spriteset_livestock_front;
    1: return spriteset_livestock_back;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_livestock, extra_callback_info1){
    1: return livestock;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, livestock){
    property{
        name: string(STR_LIVESTOCK_NAME);
        introduction_date: date(2035, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 215;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 18;
        climates_available: ALL_CLIMATES;
        default_cargo_type: LVST;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_livestock_sprites;
        articulated_part: switch_articulated_livestock;
    }
}


//Goods truck
spriteset(spriteset_goods_front, "gfx/goods.png"){
    tmpl_front(0)
}

spriteset(spriteset_goods_back, "gfx/goods.png"){
    tmpl_back(0)
}

switch(FEAT_ROADVEHS, SELF, switch_goods_sprites, position_in_articulated_veh){
    0: return spriteset_goods_front;
    1: return spriteset_goods_back;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_goods, extra_callback_info1){
    1: return goods;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, goods){
    property{
        name: string(STR_GOODS_NAME);
        introduction_date: date(2019, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 220;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 36;
        climates_available: ALL_CLIMATES;
        refittable_cargo_classes: bitmask(CC_PIECE_GOODS);
        non_refittable_cargo_classes: bitmask(CC_PASSENGERS, CC_MAIL, CC_ARMOURED, CC_LIQUID, CC_REFRIGERATED);
        cargo_allow_refit: [GOOD];
        cargo_disallow_refit: [LVST, WOOD, STEL, COPR, TWOD, ZINC, ALUM, IRON, METL, STAL, STCB, STSE, STSH, STST, STWR, PAPR];
        default_cargo_type: GOOD;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_goods_sprites;
        articulated_part: switch_articulated_goods;
    }
}


//Grain truck
switch(FEAT_ROADVEHS, SELF, switch_articulated_grain, extra_callback_info1){
    1: return grain;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, grain){
    property{
        name: string(STR_GRAIN_NAME);
        introduction_date: date(2018, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 210;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 30;
        climates_available: ALL_CLIMATES;
        cargo_allow_refit: [GRAI, WHEA, MAIZ, CERE];
        default_cargo_type: GRAI;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_mineral_sprites;
        articulated_part: switch_articulated_grain;
    }
}


//Wood truck
spriteset(spriteset_wood_front, "gfx/wood.png"){
    tmpl_front(0)
}

spriteset(spriteset_wood_back, "gfx/wood.png"){
    tmpl_back(0)
}

spriteset(spriteset_wood_front_loaded, "gfx/wood.png"){
    tmpl_front(30)
}

spriteset(spriteset_wood_back_loaded, "gfx/wood.png"){
    tmpl_back(30)
}

spritegroup spritegroup_wood_front{
    loading: [spriteset_wood_front, spriteset_wood_front_loaded];
    loaded: [spriteset_wood_front, spriteset_wood_front_loaded];
}

spritegroup spritegroup_wood_back{
    loading: [spriteset_wood_back, spriteset_wood_back_loaded];
    loaded: [spriteset_wood_back, spriteset_wood_back_loaded];
}

switch(FEAT_ROADVEHS, SELF, switch_wood_sprites, position_in_articulated_veh){
    0: return spritegroup_wood_front;
    1: return spritegroup_wood_back;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_wood, extra_callback_info1){
    1: return wood;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, wood){
    property{
        name: string(STR_WOOD_NAME);
        introduction_date: date(2029, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 222;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 24;
        climates_available: ALL_CLIMATES;
        cargo_allow_refit: [WOOD, TWOD];
        default_cargo_type: WOOD;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_wood_sprites;
        articulated_part: switch_articulated_wood;
    }
}


//Ore truck
switch(FEAT_ROADVEHS, SELF, switch_articulated_ore, extra_callback_info1){
    1: return ore;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, ore){
    property{
        name: string(STR_ORE_NAME);
        introduction_date: date(2021, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 210;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 27;
        climates_available: ALL_CLIMATES;
        cargo_allow_refit: [IORE, CORE, AORE, PORE];
        default_cargo_type: IORE;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_mineral_sprites;
        articulated_part: switch_articulated_ore;
    }
}


//Bolster truck
spriteset(spriteset_bolster_front, "gfx/bolster.png"){
    tmpl_front(0)
}

spriteset(spriteset_bolster_back, "gfx/bolster.png"){
    tmpl_back(0)
}

spriteset(spriteset_bolster_front_loaded, "gfx/bolster.png"){
    tmpl_front(30)
}

spriteset(spriteset_bolster_back_loaded, "gfx/bolster.png"){
    tmpl_back(30)
}

spriteset(spriteset_paper_front_loaded, "gfx/bolster.png"){
    tmpl_front(60)
}

spriteset(spriteset_paper_back_loaded, "gfx/bolster.png"){
    tmpl_back(60)
}

spritegroup spritegroup_bolster_front{
    loading: [spriteset_bolster_front, spriteset_bolster_front_loaded];
    loaded: [spriteset_bolster_front, spriteset_bolster_front_loaded];
}

spritegroup spritegroup_bolster_back{
    loading: [spriteset_bolster_back, spriteset_bolster_back_loaded];
    loaded: [spriteset_bolster_back, spriteset_bolster_back_loaded];
}

spritegroup spritegroup_paper_front{
    loading: [spriteset_bolster_front, spriteset_paper_front_loaded];
    loaded: [spriteset_bolster_front, spriteset_paper_front_loaded];
}

spritegroup spritegroup_paper_back{
    loading: [spriteset_bolster_back, spriteset_paper_back_loaded];
    loaded: [spriteset_bolster_back, spriteset_paper_back_loaded];
}

switch(FEAT_ROADVEHS, SELF, switch_bolster_front_sprites, cargo_type_in_veh){
    PAPR: return spritegroup_paper_front;
    return spritegroup_bolster_front;
}

switch(FEAT_ROADVEHS, SELF, switch_bolster_back_sprites, cargo_type_in_veh){
    PAPR: return spritegroup_paper_back;
    return spritegroup_bolster_back;
}

switch(FEAT_ROADVEHS, SELF, switch_bolster_sprites, position_in_articulated_veh){
    0: return switch_bolster_front_sprites;
    1: return switch_bolster_back_sprites;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_bolster, extra_callback_info1){
    1: return bolster;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, bolster){
    property{
        name: string(STR_BOLSTER_NAME);
        introduction_date: date(2019, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 222;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 20;
        climates_available: ALL_CLIMATES;
        cargo_allow_refit: [STEL, COPR, ZINC, ALUM, IRON, METL, SCMT, STAL, STCB, STSE, STSH, STST, STWR, PAPR];
        default_cargo_type: STEL;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_bolster_sprites;
        articulated_part: switch_articulated_bolster;
    }
}


//Armored truck
spriteset(spriteset_armored_front, "gfx/armored.png"){
    tmpl_front(0)
}

spriteset(spriteset_armored_back, "gfx/armored.png"){
    tmpl_back(0)
}

switch(FEAT_ROADVEHS, SELF, switch_armored_sprites, position_in_articulated_veh){
    0: return spriteset_armored_front;
    1: return spriteset_armored_back;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_armored, extra_callback_info1){
    1: return armored;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, armored){
    property{
        name: string(STR_ARMORED_NAME);
        introduction_date: date(2023, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 230;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 16;
        climates_available: ALL_CLIMATES;
        refittable_cargo_classes: bitmask(CC_ARMOURED);
        default_cargo_type: VALU;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_armored_sprites;
        articulated_part: switch_articulated_armored;
    }
}


//Food truck
spriteset(spriteset_food_front, "gfx/food.png"){
    tmpl_front(0)
}

spriteset(spriteset_food_back, "gfx/food.png"){
    tmpl_back(0)
}

switch(FEAT_ROADVEHS, SELF, switch_food_sprites, position_in_articulated_veh){
    0: return spriteset_food_front;
    1: return spriteset_food_back;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_food, extra_callback_info1){
    1: return food;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, food){
    property{
        name: string(STR_FOOD_NAME);
        introduction_date: date(2014, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 222;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 22;
        climates_available: ALL_CLIMATES;
        refittable_cargo_classes: bitmask(CC_REFRIGERATED);
        non_refittable_cargo_classes: bitmask(CC_LIQUID);
        cargo_disallow_refit: [FRUT, FRVG];
        default_cargo_type: FOOD;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_food_sprites;
        articulated_part: switch_articulated_food;
    }
}


//Paper truck
switch(FEAT_ROADVEHS, SELF, switch_articulated_paper, extra_callback_info1){
    1: return paper;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, paper){
    property{
        name: string(STR_PAPER_NAME);
        introduction_date: date(2022, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 222;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 20;
        climates_available: ALL_CLIMATES;
        default_cargo_type: PAPR;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_bolster_sprites;
        articulated_part: switch_articulated_paper;
    }
}


//Water truck
switch(FEAT_ROADVEHS, SELF, switch_articulated_water, extra_callback_info1){
    1: return water;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, water){
    property{
        name: string(STR_WATER_NAME);
        introduction_date: date(2027, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 220;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 30;
        climates_available: ALL_CLIMATES;
        default_cargo_type: WATR;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_tanker_sprites;
        articulated_part: switch_articulated_water;
    }
}


//Fruit truck
spriteset(spriteset_fruit_front, "gfx/fruit.png"){
    tmpl_front(0)
}

spriteset(spriteset_fruit_back, "gfx/fruit.png"){
    tmpl_back(0)
}

spriteset(spriteset_fruit_front_loaded, "gfx/fruit.png"){
    tmpl_front(30)
}

spriteset(spriteset_fruit_back_loaded, "gfx/fruit.png"){
    tmpl_back(30)
}

spritegroup spritegroup_fruit_front{
    loading: [spriteset_fruit_front, spriteset_fruit_front_loaded];
    loaded: [spriteset_fruit_front, spriteset_fruit_front_loaded];
}

spritegroup spritegroup_fruit_back{
    loading: [spriteset_fruit_back, spriteset_fruit_back_loaded];
    loaded: [spriteset_fruit_back, spriteset_fruit_back_loaded];
}

switch(FEAT_ROADVEHS, SELF, switch_fruit_sprites, position_in_articulated_veh){
    0: return spritegroup_fruit_front;
    1: return spritegroup_fruit_back;
}

switch(FEAT_ROADVEHS, SELF, switch_articulated_fruit, extra_callback_info1){
    1: return fruit;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, fruit){
    property{
        name: string(STR_FRUIT_NAME);
        introduction_date: date(2024, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 15;
        loading_speed: 5;
        cost_factor: 225;
        running_cost_factor: 255 / (param_adjust_running_cost + 1);
        speed: 90 km/h;
        power: 500 hp;
        weight: 25 ton;
        cargo_capacity: 23;
        climates_available: ALL_CLIMATES;
        cargo_allow_refit: [FRUT, FRVG, SGCN];
        default_cargo_type: FRUT;
        refit_cost: 4;
        running_cost_base: RUNNING_COST_ROADVEH;
        sprite_id: SPRITE_ID_NEW_ROADVEH;
    }
    graphics{
        default: switch_fruit_sprites;
        articulated_part: switch_articulated_fruit;
    }
}


//Reduce running cost for original road vehicles if the parameter is activated
if(param_adjust_running_cost == 1){
    item(FEAT_ROADVEHS, original_bus_1, 0){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_bus_2, 1){
        property{
            running_cost_factor: 65;
        }
    }
    item(FEAT_ROADVEHS, original_bus_3, 2){
        property{
            running_cost_factor: 90;
        }
    }
    item(FEAT_ROADVEHS, original_bus_4, 3){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_coal_1, 7){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_coal_2, 8){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_coal_3, 9){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_mail_1, 10){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_mail_2, 11){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_mail_3, 12){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_oil_1, 16){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_oil_2, 17){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_oil_3, 18){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_livestock_1, 19){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_livestock_2, 20){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_livestock_3, 21){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_goods_1, 22){
        property{
            running_cost_factor: 45;
            cargo_capacity: 28;
        }
    }
    item(FEAT_ROADVEHS, original_goods_2, 23){
        property{
            running_cost_factor: 85;
            cargo_capacity: 32;
        }
    }
    item(FEAT_ROADVEHS, original_goods_3, 24){
        property{
            running_cost_factor: 120;
            cargo_capacity: 36;
        }
    }

    item(FEAT_ROADVEHS, original_grain_1, 25){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_grain_2, 26){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_grain_3, 27){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_wood_1, 28){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_wood_2, 29){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_wood_3, 30){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_iron_ore_1, 31){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_iron_ore_2, 32){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_iron_ore_3, 33){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_steel_1, 34){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_steel_2, 35){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_steel_3, 36){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_armored_1, 37){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_armored_2, 38){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_armored_3, 39){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_food_1, 40){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_food_2, 41){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_food_3, 42){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_paper_1, 43){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_paper_2, 44){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_paper_3, 45){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_copper_ore_1, 46){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_copper_ore_2, 47){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_copper_ore_3, 48){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_water_1, 49){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_water_2, 50){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_water_3, 51){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_fruit_1, 52){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_fruit_2, 53){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_fruit_3, 54){
        property{
            running_cost_factor: 120;
        }
    }

    item(FEAT_ROADVEHS, original_rubber_1, 55){
        property{
            running_cost_factor: 45;
        }
    }
    item(FEAT_ROADVEHS, original_rubber_2, 56){
        property{
            running_cost_factor: 85;
        }
    }
    item(FEAT_ROADVEHS, original_rubber_3, 57){
        property{
            running_cost_factor: 120;
        }
    }
}